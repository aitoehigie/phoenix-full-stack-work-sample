defmodule FlyWeb.AppLive.Status do
  use FlyWeb, :live_component
  require Logger

  alias Fly.Client

  @refresh_rate 3000

  def mount(%{"name" => name}, session, socket) do
    socket =
      assign(socket,
        config: client_config(session),
        state: :loading,
        app: nil,
        app_status: nil,
        app_name: name,
        count: 0,
        authenticated: true
      )

    # Only make the API call if the websocket is setup. Not on initial render.
    if connected?(socket) do
      start_refresh(@refresh_rate)
      {:ok, fetch_app(socket)}
    else
      {:ok, socket}
    end
  end

  defp client_config(session) do
    Fly.Client.config(access_token: session["auth_token"] || System.get_env("FLYIO_ACCESS_TOKEN"))
  end

  defp fetch_app(socket) do
    app_name = socket.assigns.app_name

    case Client.fetch_app(app_name, socket.assigns.config) do
      {:ok, _app} ->
        {:ok, app_status} = Client.fetch_status(app_name, socket.assigns.config)
        assign(socket, app_status: app_status)

      {:error, :unauthorized} ->
        put_flash(socket, :error, "Not authenticated")

      {:error, reason} ->
        Logger.error("Failed to load app '#{inspect(app_name)}'. Reason: #{inspect(reason)}")

        put_flash(socket, :error, reason)
    end
  end

  @impl true
  def handle_event("click", _params, socket) do
    {:noreply, assign(socket, count: socket.assigns.count + 1)}
  end

  def status_bg_color(app) do
    case app["status"] do
      "running" -> "bg-green-100"
      "dead" -> "bg-red-100"
      _ -> "bg-yellow-100"
    end
  end

  def status_text_color(app) do
    case app["status"] do
      "running" -> "text-green-800"
      "dead" -> "text-red-800"
      _ -> "text-yellow-800"
    end
  end

  @impl true
  def handle_info(:clock_tick, socket) do
    {:noreply, fetch_app(socket)}
  end

  defp start_refresh(refresh_rate) do
    :timer.send_interval(refresh_rate, self(), :clock_tick)
  end

  def preview_url(app) do
    "https://#{app["name"]}.fly.dev"
  end
end
